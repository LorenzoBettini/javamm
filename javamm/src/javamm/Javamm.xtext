grammar javamm.Javamm with org.eclipse.xtext.xbase.Xbase

//generate javamm "http://www.Javamm.javamm"
import "http://www.Javamm.javamm"

import "http://www.eclipse.org/xtext/xbase/Xbase"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

JavammProgram:
	(javammMethods+=JavammMethod)*
	main=Main
;

JavammMethod:
	=>({JavammMethod} type=JvmTypeReference name=ValidID 
		'(') (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' 
			body=XBlockExpression;

/* 
 * The ';' only "eats" possible additional semicolons which is legal in Java.
 * The required ';' is taken care of in the JavammSemicolonStatement rule
 */
Main returns XBlockExpression:
	{Main}
	(expressions+=JavammStatementOrBlock ';'*)*;

JavammStatementOrBlock returns XExpression:
//	JavammArrayConstructorCall | not a valid statement
	=>JavammSingleStatement |
	XBlockExpression
;

JavammSingleStatement returns XExpression:
	JavammSemicolonStatement |
	XSwitchExpression |
	XIfExpression |
	XForLoopExpression |
	XBasicForLoopExpression |
	XWhileExpression
;

/*
 * These are the Java statements that require a semicolon at the end
 */
JavammSemicolonStatement returns XExpression:
//	JavammArrayConstructorCall | not a valid statement
	(=>XVariableDeclaration |
	XExpression |
	XDoWhileExpression |
	XReturnExpression) ';'
;

/* 
 * The ';' only "eats" possible additional semicolons which is legal in Java.
 * The required ';' is taken care of in the JavammSemicolonStatement rule
 */
XBlockExpression returns XExpression: 
	{XBlockExpression}
	'{'
		(expressions+=JavammStatementOrBlock ';'*)*
	'}';

XExpressionOrVarDeclaration returns XExpression:
	=>XVariableDeclaration | XExpression;

XVariableDeclaration returns XVariableDeclaration:
	{JavammXVariableDeclaration}
	type=JvmTypeReference name=ValidID ('=' right=XExpression)?;

XAssignment returns XExpression :
	=>({JavammXAssignment} feature=[types::JvmIdentifiableElement|FeatureCallID] 
		'[' index=XExpression ']'
		OpSingleAssign) value=XAssignment |	
	=>({XAssignment} feature=[types::JvmIdentifiableElement|FeatureCallID] 
		OpSingleAssign) value=XAssignment |	
	XOrExpression (
		=>({XBinaryOperation.leftOperand=current} feature=[types::JvmIdentifiableElement|OpMultiAssign]) rightOperand=XAssignment
	)?;

XFeatureCall returns XExpression:
	{XFeatureCall}
	feature=[types::JvmIdentifiableElement|IdOrSuper] 
	(=>explicitOperationCall?='(' 
		(
		    featureCallArguments+=XExpression (',' featureCallArguments+=XExpression)*
		)? 
	')' 
	)?;

XConstructorCall returns XExpression:
	{XConstructorCall}
	'new' constructor=[types::JvmConstructor|QualifiedName] 
	(=>explicitConstructorCall?='(' 
		(
		    arguments+=XExpression (',' arguments+=XExpression)*
		)? 
	')')?;

JavammArrayConstructorCall returns XExpression:
	=>({JavammArrayConstructorCall}
	'new'
	type=[types::JvmType|QualifiedName] '[') index=XExpression ']'
;

XPostfixOperation returns XExpression:
	XMemberFeatureCall
	(
		=>({XPostfixOperation.operand=current} feature=[types::JvmIdentifiableElement|OpPostfix])
		|
		=>({JavammArrayAccessExpression.array=current} '[' index=XExpression ']')
	)?
;

XMemberFeatureCall returns XExpression:
	XPrimaryExpression
	(
		=>({XAssignment.assignable=current} ('.'|explicitStatic?="::") 
			feature=[types::JvmIdentifiableElement|FeatureCallID] OpSingleAssign)
				value=XAssignment
	|
		=>({XMemberFeatureCall.memberCallTarget=current} ".") 
			feature=[types::JvmIdentifiableElement|IdOrSuper] (
				=>explicitOperationCall?='(' 
					(
					    memberCallArguments+=XExpression (',' memberCallArguments+=XExpression)*
					)? 
				')')?
	)*;

XBasicForLoopExpression returns XExpression:
	{XBasicForLoopExpression}
	'for' '('(initExpressions+=XExpressionOrVarDeclaration (',' initExpressions+=XExpressionOrVarDeclaration)*)? ';' 
		expression=XExpression? ';' 
		(updateExpressions+=XExpression (',' updateExpressions+=XExpression)*)? ')'
		eachExpression=JavammStatementOrBlock;

XIfExpression returns XExpression:
	{XIfExpression}
	'if' '(' if=XExpression ')'
	then=JavammStatementOrBlock
	(=>'else' else=JavammStatementOrBlock)?;

XWhileExpression returns XExpression:
	{XWhileExpression}
	'while' '(' predicate=XExpression ')'
		body=JavammStatementOrBlock;

XDoWhileExpression returns XExpression:
	{XDoWhileExpression}
   'do'
       body=JavammStatementOrBlock
   'while' '(' predicate=XExpression ')';

XPrimaryExpression returns XExpression:
	JavammArrayConstructorCall |
	XConstructorCall |
//	XBlockExpression |
//	XSwitchExpression |
//	XSynchronizedExpression |
	XFeatureCall |
	XLiteral |
//	XIfExpression |
//	XForLoopExpression |
//	XBasicForLoopExpression |
//	XWhileExpression |
//	XDoWhileExpression |
//	XThrowExpression |
//	XReturnExpression |
//	XTryCatchFinallyExpression |
	XParenthesizedExpression;

XLiteral returns XExpression:
//	XCollectionLiteral |
//	XClosure |
	XBooleanLiteral |
	XNumberLiteral |
	XNullLiteral |
	XStringLiteral |
	JavammArrayLiteral
//	XTypeLiteral
;

JavammArrayLiteral returns XListLiteral:
	{JavammArrayLiteral} '{' (elements+=XExpression (',' elements+=XExpression )*)? '}'
;
